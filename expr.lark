%import common.INT -> INT
%import common.CNAME -> ID
%import common.WS

%ignore WS

_TRUE: "true"
_FALSE: "false"

?expr: expr1 (";" expr1)* -> seq

?expr1: "ifnz" expr "then" expr "else" expr -> ifnz
     | expr2

?expr2: blurexp
     | invertexp
     | rotateexp
     | combineexp
     | lightenexp
     | darkenexp
     | expr2 "show" -> show
     | expr2 ":=" term -> assign
     | expr2 "&&" term -> andexpr
     | expr2 "||" term -> orexpr
     | expr2 "==" term -> eqexpr
     | expr2 "<" term -> lessexpr
     | expr2 "+" term  -> plus
     | expr2 "-" term -> minus
     | term

blurexp: "blur" "(" expr ")"
invertexp: "invert" "(" expr ")"
rotateexp: "rotate" "(" expr ")"
combineexp: "combine" "(" expr "," expr ")"
lightenexp: "lighten" "(" expr ")"
darkenexp: "darken" "(" expr ")"

?term: term "*" factor -> times
     | term "/" factor -> divide
     | factor

?factor:"!" factor -> notexpr
     | "-" factor  -> neg
     |  atom

?args: (expr ("," expr)*)? -> args

?params: (ID ("," ID)*)? -> params

?atom: _TRUE -> true 
     | _FALSE -> false
     | ID  -> id
     | INT -> int
     | atom "(" args ")" -> app
     | "(" expr ")"    
     | "let" ID "=" expr "in" expr "end" -> let
     | "letfun" ID "(" params ")" "=" expr "in" expr "end" -> letfun

start: expr

